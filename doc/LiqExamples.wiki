=====Cookbook=====

The recipes show how to build a source with a particular feature. You can try short snippets by wrapping the code in an ##out(..)## operator and passing it directly to liquidsoap:
%%(sh)
liquidsoap -v 'out(recipe)'
%%

For longer recipes, you might want to create a short script:
%%(liquidsoap)
#!/usr/bin/liquidsoap -v

set log.dir = "/tmp"
set log.stdout = true

recipe = # <fill this>
out(recipe)
%%

See the [[LiqQuickstart quickstart guide]] for more information on how to run liquidsoap, on what is this ##out(..)## operator, etc.

====Files====

A source which infinitely repeats the same URI:
%%(liquidsoap)
single("/my/default.ogg")
%%

A source which plays a playlist of requests -- a playlist is a file with an URI per line.
%%(liquidsoap)
# Shuffle, play every URI, start over.
playlist("/my/playlist.txt")
# Do not randomize
playlist(mode="normal", "/my/pl.m3u")
# The playlist can come from any URI, can be reloaded every 10 minutes.
playlist(reload=600,"http://my/playlist.txt")
%%

When building your stream, you'll need to make it unfallible. Usually, you achieve that using a fallback switch (see below) with a branch made of a safe ##single## or ##playlist.safe##. Roughly, a single is safe when it is given a valid local audio file. A ##playlist.safe## behaves just like a playlist but will check that all files in the playlist are valid local audio files. This is quite an heavy check, you don't want to have large safe playlists.

====Scheduling====

%%(liquidsoap)
# A fallback switch
fallback([playlist("http://my/playlist"), single("/my/jingle.ogg")])
# A scheduler, assuming you have defined the night and day sources
switch([ ({0h-7h}, night), ({7h-24h}, day) ])
%%

====Force a file/playlist to be played at least every XX minutes====
It can be useful to have a special playlist that is played at least every 20 minutes for instance (3 times per hour).
You may think of a promotional playlist for instance.
Here is the recipe:
%%(liquidsoap)
timed_promotions = delay(1200.,promotions) # 1200 sec = 20 min
main_source = fallback([timed_promotions,other_source])
%%
Where promotions is a source selecting the file to be promoted.

====Handle special events: mix or switch====

%%(liquidsoap)
# Add a jingle to your normal source at the beginning of every hour:
add([normal,switch([({0m0s},jingle)])])
%%

Switch to a live show as soon as one is available. Make the show unavailable when it is silent, and skip tracks from the normal source if they contain too much silence.
%%(liquidsoap)
fallback(track_sensitive=false,
	     [strip_blank(input.http("http://myicecast:8080/live.ogg")),
	      skip_blank(normal)])
%%

Without the ##track_sensitive=false## the fallback would wait the end of a track to switch to the live. When using the blank detection operators, make sure to fine-tune their ##threshold## and ##length## (float) parameters.

====Unix interface, dynamic requests====

##request.dynamic## is a source which takes a custom function for creating its new requests. This function can be used to call an external program. The source expects a ##()->request## function. To create the request, the function will have to use the ##request## function which has type ##(string,?indicators:[string])##. The first string is the initial URI of the request, which is resolved to get an audio file. The second argument can be used to directly specify the first row of URIs (see the [[LiqConcepts concepts page]]), in which case the initial URI is just here for naming, and the resolving process will try your list of indicators one by one until a valid audio file is obtained.

The simplest example takes the output of an external script as an URI to create a new request:
%%(liquidsoap)
request.dynamic({ request(get_process_output("my_script my_params")) })
%%

More complex, the following snippet defines a source which repeatedly plays the first valid URI in the playlist:
%%(liquidsoap)
request.dynamic({ request("bar:foo",
	                indicators=get_process_lines("cat "^quote("playlist.pls"))) })
%%
Of course a more interesting behaviour is obtained with a more interesting program than "cat".

Another way of using an external program is to define a new protocol which uses it to resolve URIs. ##add_protocol## takes a protocol name, a function to be used for resolving URIs using that protocol. The function will be given the URI parameter part and the time left for resolving -- though nothing really bad happens if you don't respect it. It usually passes the parameter to an external program, that's how we use [[bubble]] for example:
%%(liquidsoap)
add_protocol("bubble",
	     fun (arg,delay) -> get_process_lines("/usr/bin/bubble-query "^quote(arg)))
%%
When resolving the URI ##bubble:artist="seed"##, liquidsoap will call the function, which will call ##bubble-query 'artist="seed"'## which will output 10 lines, one URI per line.

====Transitions====

There are two kinds of transitions. Transitions between two different children of a switch are not problematic. Transitions between different tracks of the same source are more tricky, since they involve a fast forward computation of the end of a track before feeding it to the transition function: such a thing is only possible when only one operator is using the source, otherwise it'll get out of sync.

===Switch-based transitions===

The switch-based operators (##switch##, ##fallback## and ##random##) support transitions. For every child, you can specify a transition function computing the output stream when moving from one child to another. This function is given two ##source## parameters: the child which is about to be left, and the new selected child. The default transition is ##fun (a,b) -> b##, it simply relays the new selected child source. Other possible transition functions:
%%(liquidsoap)
# A simple (long) cross-fade
def crossfade(a,b)
  add(normalize=false,
	  [ sequence([ blank(duration=5.),
	               fade.initial(duration=10.,b) ]),
	    fade.final(duration=10.,a) ])
end

# Partially apply next to give it a jingle source.
# It will fade out the old source, then play the jingle.
# At the same time it fades in the new source.
def next(j,a,b)
  add(normalize=false,
	  [ sequence(merge=true,
	             [ blank(duration=3.),
	               fade.initial(duration=6.,b) ]),
	    sequence([fade.final(duration=9.,a),
	              j,fallback([])]) ])
end

# A similar transition, which does a cross-fading from A to B
# and adds a jingle
def transition(j,a,b)
  add(normalize=false,
	  [ fade.initial(b),
	    sequence(merge=true,
	            [blank(duration=1.),j,fallback([])]),
	    fade.final(a) ])
end
%%

Finally, we build a source which plays a playlist, and switches to the live show as soon as it starts, using the ##transition## function as a transition. At the end of the live, the playlist comes back with a cross-fading.
%%(liquidsoap)
fallback(track_sensitive=false,
	     transitions=[ crossfade, transition(jingle) ],
	     [ input.http("http://localhost:8000/live.ogg"),
	       playlist("playlist.pls") ])
%%

===Cross-based transitions===

The ##cross()## operator allows arbitrary transitions between tracks of a same source. Here is how to use it in order to get a cross-fade:
%%(liquidsoap)
def crossfade(~start_next,~fade_in,~fade_out,s)
  s = fade.in(duration=fade_in,s)
  s = fade.out(duration=fade_out,s)
  fader = fun (a,b) -> add(normalize=false,[b,a])
  cross(fader,s)
end
my_source=crossfade(start_next=1.,fade_out=1.,fade_in=1.,my_source)
%%
The fade-in and fade-out parameters indicate the duraction of the fading effects. The start-next parameters tells how much overlap there will be between the two tracks. If you want a long cross-fading with a smaller overlap, you should use a sequence to stick some blank section before the beginning of ##b## in ##fader##.
The three parameters given here are only default values, and will be overriden by values coming from the metadata tags ##liq_fade_in##, ##liq_fade_out## and ##liq_start_next##.

===How to get transitions on a mix?===

If you ##add()## a special source on top of your normal stream, you might notice that the re-normalization is not smooth at all: if the special source suddenly becomes available, the normal one will be re-normalized immediately, which is not very nice to hear, especially if the special source starts with a low noise level. The ##add()## operator does not support transitions but there is a solution for this kind of situation. Use a ##fallback()## in order to get transitions, and simply keep playing the normal source in the transition. Here is the code.

%%(liquidsoap)
def smooth_add(~normal,~special)
  d = 1.  # delay before mixing after beginning of mix
  p = 0.2 # portion of normal when mixed
  fade.final = fade.final(duration=d*.2.)
  fade.initial = fade.initial(duration=d*.2.)
  q = 1. -. p
  c = change_volume
  fallback(track_sensitive=false,
	       [special,normal],
	       transitions=[
	         fun(normal,special)->
	           add(normalize=false,[c(p,normal),
	                                c(q,fade.final(normal)),
	                                sequence([blank(duration=d),c(q,special)])]),
	         fun(special,normal)->
	           add(normalize=false,[c(p,normal),c(q,fade.initial(normal))])
	       ])
end
%%

The first transition is used when the special source becomes available. It sums the special source (after a delay ##d##) together with a reduced version of ##normal## (##c(p,normal)##) and its faded-out complement (##c(q,normal)##). As a result the amplitude of ##normal## will smoothly move from ##1=p+q## down to ##p##.

The second transition is called when ##special## becomes unavailable. This time, the reduced version of ##normal## is mixed with its faded-in complement.

==== Alsa unbuffered output ====

You can use liquidsoap to capture and play through alsa with a minimal delay. This particulary usefull when you want to run a live show from your computer. You can then directly capture and play audio through external speakers without delay for the DJ !

This configuration is not trivial since it relies on your hardware. Some hardware will allow both recording and playing at the same time, some only one at once, and some none at all.. Those note to configure are what works for us, we don't know if they'll fit all hardware.

First launch liquidsoap as a one line program
%%
liquidsoap -v --debug 'input.alsa(bufferize=false)'
%%
Unless you're lucky, the logs are full of lines like the following:
%%
Partial read (940 instead of 1024)! Selecting another buffer size or device can help.
%%

The solution is then to fix the captured frame size to this value, which seems specific to your hardware. Let's try this script:
%%(liquidsoap)

# Set correct frame size:
set frame.size = 940

input = input.alsa(bufferize=false)
output.alsa(bufferize=false,input)
%%
If everything goes right, you may hear on your output the captured sound without any delay ! If you want to test the difference, just run the same script with bufferize=true (or without this parameter since it is the default).

If you experience problems it might be a good idea to double the value of the frame size. This increases stability, but also latency.